pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url 'https://repo.spring.io/plugins-release' }
    }
}

plugins {
    id "com.gradle.enterprise" version "3.6.1"
    id "io.spring.ge.conventions" version "0.0.7"
}

include "demo-core"
include "spring-boot"
include "spring-framework"

rootProject.name = "demo-api"
rootProject.children.each {project ->
    project.buildFileName = "${project.name}.gradle"
}

settings.gradle.projectsLoaded {
    gradleEnterprise {
        buildScan {
            File buildDir = settings.gradle.rootProject.getBuildDir()
            buildDir.mkdirs()
            new File(buildDir, "build-scan-uri.txt").text = "(build scan not generated)"
            buildScanPublished { scan ->
                if (buildDir.exists()) {
                    new File(buildDir, "build-scan-uri.txt").text = "${scan.buildScanUri}\n"
                }
            }
        }
    }
}